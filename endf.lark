%import common.DIGIT
%import common.NEWLINE
%import common.STRING
%import common.CNAME
%import common.INT
%import common.NUMBER
%ignore " "

endf_format : mt_sec
mt_sec : NEWLINE* head_line (NEWLINE | head_line | cont_line | tab1_line)* send_line

code_token: (endf_line | for_loop | NEWLINE)*
endf_line : head_line | cont_line | tab1_line
            | text_line | dir_line  | send_line

// control numbers
ctrl_spec : MAT_SPEC "," MF_SPEC "," MT_SPEC
MAT_SPEC : "MAT" | INT
MF_SPEC :  "MF" | INT
MT_SPEC : "MT" | INT

// TEXT record
text_line : "[" ctrl_spec "/" text_fields "]" "TEXT" NEWLINE*
text_fields : expr

// HEAD record
head_line : "[" ctrl_spec "/" head_fields "]" "HEAD" NEWLINE*
head_fields : expr "," expr "," expr "," expr "," expr "," expr

// CONT record
cont_line : "[" ctrl_spec "/" cont_fields "]" "CONT" NEWLINE*
cont_fields : expr "," expr "," expr "," expr "," expr "," expr

// DIR record
dir_line : "[" ctrl_spec "/" "blank" "," "blank" "," dir_fields "]" "DIR" NEWLINE*
dir_fields :  expr "," expr "," expr "," expr

// TAB1 record
tab1_line : "[" ctrl_spec "/" tab1_fields "]" "TAB1" ("(" table_name ")")? NEWLINE*
tab1_fields : tab1_cont_fields "/" tab1_def
tab1_cont_fields : CFIELD "," CFIELD "," IFIELD "," IFIELD "," IFIELD "," IFIELD
tab1_def : CNAME "/" CNAME
table_name : CNAME

// SEND record
send_line : "SEND" NEWLINE*

// FOR loop
for_loop : "for" for_head for_body "endfor"
for_head : VARNAME "=" for_start "to" for_stop ":"
for_body : NEWLINE* code_token* NEWLINE*
for_start :  expr
for_stop :   expr

// arithmetic expression
// adopted from: http://marvin.cs.uidaho.edu/Teaching/CS445/grammar.pdf (3.3)
expr : addition | subtraction | addpart
addpart : multiplication | division | mulpart
mulpart : minusexpr | extvarname | NUMBER | bracketexpr

multiplication : addpart "*" mulpart
division : addpart "/" mulpart
addition : expr "+" addpart
subtraction : expr "-" addpart

minusexpr: "-" mulpart
bracketexpr : "(" expr ")"

// allowed variable names (including indices)
extvarname : VARNAME ("[" INDEXVAR "]")?
VARNAME : CNAME
INDEXVAR : CNAME

// possible field values
CFIELD: CNAME | "0.0"
IFIELD: CNAME | "0"

