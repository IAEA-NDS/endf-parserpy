############################################################
#
# Author(s):       Georg Schnabel
# Email:           g.schnabel@iaea.org
# Creation date:   2022/05/30
# Last modified:   2024/04/26
# License:         MIT
# Copyright (c) 2022-2024 International Atomic Energy Agency (IAEA)
#
############################################################

ENDF_RECIPE_MF1_MT451 = """

# var ZA:      ZA = (1000.0 * Z) + A
#              Z ... charge number of material
#              A ... mass number of material
# var AWR:     ratio of the mass of the material to that of the neutron
# var LRP:     Flag indicating whether resolved and/or unresolved
#              resonance parameters are given in File 2
#              LRP=-1, no File 2 is given (not allowed for incident neutrons);
#              LRP=0, no resonance parameter data are given, but a File 2
#              is present containing the effective scattering radius;
#              LRP=1, resolved and/or unresolved parameter data are given
#              in File 2 and cross sections computed from them must be added
#              to background cross sections given in File 3;
#              LRP=2, parameters are given in File 2, but cross sections derived
#              from them are not to be added to the cross sections in File 3. The
#              option LRP=2 is to be used for derived files only and is typical in
#              the so-called PENDF files, in which the cross sections are already
#              reconstructed from the resonances parameters and written in File 3.
# var LFI:     Flag indicating whether this material fissions:
#              LFI=0, this material does not fission;
#              LFI=1, this material fissions.
# var NLIB:    Library identifier (e.g. NLIB= 0 for ENDF/B). Additional
#              values have been assigned to identify other libraries using
#              ENDF format. See Section 0.3.1 for details.
# var NMOD:    Modification number for this material:
#              NMOD=0, evaluation converted from a previous version;
#              NMOD=1, new or revised evaluation for the current library version;
#              NMOD≥ 2, for successive modifications
# var ELIS:    Excitation energy of the target nucleus relative to 0.0 for the
#              ground state.
# var STA:     Target stability flag:
#              STA=0, stable nucleus;
#              STA=1 unstable nucleus. If the target is unstable, radioactive decay data
#              should be given in the decay data sub-library (NSUB=4).
# var LIS:     State number of the target nucleus. The ground state is indicated by LIS=0.
# var LISO:    Isomeric state number. The ground state is indicated by LISO=0. LIS is
#              greater than or equal to LISO.
# var NFOR:    Library format.
#              NFOR=6 for all libraries prepared according to the specifications
#              given in this manual.
# var AWI:     Mass of the projectile in neutron mass units. For incident photons or decay
#              data sub-libraries, use AWI=0.
# var EMAX:    Upper limit of the energy range for evaluation.
# var LREL:    Library release number; for example, LREL=2 for the ENDF/B-VI.2 library.
# var NSUB:    Sub-library number. See Table 3 in Section 0.3.1 for a description
#              of sublibraries.
# var NVER:    Library version number; for example, NVER=7 for version ENDF/B-VII.
# var TEMP:    Target temperature (Kelvin) for data that have been generated by
#              Doppler broadening. For derived data only; use TEMP=0.0 for all
#              primary evaluations.
# var LDRV:    Special derived material flag that distinguishes between different evaluations
#              with the same material keys (i.e., MAT, NMOD, NSUB):
#              LDRV=0, primary evaluation:
#              LDRV≥ 1, special derived evaluation (for example, a dosimetry evaluation
#              using sections (MT) extracted from the primary evaluation).
# var NWD:     Number of records with descriptive text for this material.
#              Each record contains up to 66 characters.
# var NXC:     Number of records in the directory for this material. Each section (MT) in
#              the material has a corresponding line in the directory that contains MF, MT,
#              NC, and MOD. NC is a count of the number of records in the section (not
#              including SEND), and MOD is the modification flag (see below).
# var ZSYMAM:  Character representation of the material defined by the atomic number,
#              chemical symbol, atomic mass number, and metastable state
#              designation in the form Z-cc-AM with Z, right justified in col. 1 to 3,
#              - (hyphen) in col. 4,
#              cc, two-character chemical name left justified in col. 5 and 6,
#              - (hyphen) in col. 7,
#              A, right justified in col. 8 to 10 or blank,
#              M for the indication of a metastable state in col. 11,
#              for example, 1-H - 2, 40-Zr- 90, 95-Am-242M, etc
# var ALAB:    Mnemonic for the originating laboratory(s) left adjusted in col. 12-22.
# var EDATE:   Date of evaluation given in the form “EVAL-DEC74” in col. 23-32.
# var AUTH:    Author(s) name(s) left adjusted in col. 34-66.
# var REF:     Primary reference for the evaluation left adjusted in col. 2-22.
# var DDATE:   Original distribution date given in the form “DIST-DEC74” in col. 23-32.
# var RDATE:   Date and number of the last revision to this evaluation in col. 34-43 in the
#              form “REV2-DEC74”, where “2” in column 37 is the release number and must
#              be equal to LREL. The term “revision” (IREV), which appeared in earlier
#              editions of this manual, was synonymous with “release” (LREL) and has
#              been dropped in favor of the more-frequently used term “release”.
# var ENDATE:  Master File entry date in the form yyyymmdd right adjusted in col. 56-63.
#              The Master File entry date is assigned by NNDC for ENDF/B libraries.
# var HSUB:    Identifier for the library contained on three successive records is useful for
#              visual inspection of the files and is followed strictly in the ENDF/B libraries.
#              The first record contains four dashes starting in col. 1, directly followed by
#              the library type (NLIB) and version (NVER). For example,
#              “---- ENDF/B-VI”, followed by MATERIAL XXXX starting in col. 23 where
#              XXXX is the MAT number, and REVISION 2 (starting in col. 45 only if
#              required) where “2” is the revision number IREV.
#              The second record contains five dashes starting in col. 1, and followed by the
#              sub-library identifier (see Table 3). For example,
#              “----- DECAY DATA”,
#              “----- PHOTO-ATOMIC INTERACTION DATA” or
#              “----- INCIDENT NEUTRON DATA”
#              The third record contains six dashes starting in col. 1 and followed by ENDF-6
#              where “6” is the library format type (NFOR).
#              Note: the three HSUB records can be generated by the utility program,
#              STANEF.
# var MFx[i]:  ENDF file number of the i-th section
# var MTx[i]:  ENDF reaction designation of the i-th section.
# var NCx[i]:  Number of records in the i-th section.
#              This count does not include the SEND record.
# var MOD[i]:  Modification indicator for the i-th section.
#              The value of MOD[i] is equal to NMOD if the corresponding section
#              was changed in this revision. MOD[i] must always be less than
#              or equal to NMOD.
# var DESCRIPTION[i]: Lines with descriptive text


[MAT, 1,451/ ZA, AWR, LRP, LFI, NLIB, NMOD]HEAD
[MAT, 1,451/ ELIS, STA, LIS, LISO, 0, NFOR]CONT
[MAT, 1,451/ AWI, EMAX, LREL, 0, NSUB, NVER]CONT
[MAT, 1,451/ TEMP, 0.0, LDRV, 0, NWD, NXC]CONT

[MAT, 1,451/ ZSYMAM{11}, ALAB{11}, EDATE{10}, {1}, AUTH{33} ]TEXT
[MAT, 1,451/ {1}, REF{21}, DDATE{10}, {1},
             RDATE{10}, {12}, ENDATE{8}, {3} ]TEXT
for i=1 to 3:
    [MAT, 1,451/ HSUB[i]] TEXT
endfor
for i=1 to NWD-5:
    [MAT, 1,451/ DESCRIPTION[i]]TEXT
endfor
for i=1 to NXC:
    [MAT, 1,451/ blank, blank, MFx[i], MTx[i], NCx[i], MOD[i]]DIR
endfor
SEND
"""
